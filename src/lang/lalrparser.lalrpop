use std::str::FromStr;
use crate::lang::ast::*;

grammar;

pub JobList: JobListNode = {
    Separator? <l:JobListWithoutSeparator> => l,
};

JobListWithoutSeparator: JobListNode = {
    => JobListNode {jobs: vec![]},
    <j: NonEmptyJobList> Separator? => j,
};

NonEmptyJobList: JobListNode = {
    <mut l:NonEmptyJobList> Separator <j:Job> =>  {l.jobs.push(j); l},
    Job => JobListNode {jobs: vec![<>]},
};

Job: JobNode = {
    Command => JobNode{commands: vec![<>]},
    <mut j:Job> "|" Separator? <c:Command> => {j.commands.push(c); j}
};

Command: CommandNode = {
    Assignment => CommandNode{expressions: vec![*<>]},
    <mut c: Command> <a:Assignment> => {c.expressions.push(*a); c}
};

Assignment: Box<Node> = {
    Logical,
    <i: Item> "=" <e: Assignment> => Box::from(Node::Assignment(i, "=".to_string(), e)),
    <i: Item> ":=" <e: Assignment> => Box::from(Node::Assignment(i, ":=".to_string(), e)),
}

Logical: Box<Node> = {
    Comparison,
    <l: Logical> <op: LogicalOperator> <c: Comparison> => Box::from(Node::LogicalOperation(l, op.to_string(), c)),
}

Comparison: Box<Node> = {
    Term,
    <c: Comparison> <op: ComparisonOperator> <t: Term> => Box::from(Node::Comparison(c, op.to_string(), t)),
}

Term: Box<Node> = {
    Factor,
    <t: Term> <op: TermOperator> <f: Factor> => Box::from(Node::Term(t, op.to_string(), f)),
}

Factor: Box<Node> = {
    Unary,
    <f: Factor> <op: FactorOperator> <u: Unary> => Box::from(Node::Factor(f, op.to_string(), u)),
}

Unary: Box<Node> = {
    Item,
    <op: UnaryOperator> <u: Unary> => Box::from(Node::Unary(op.to_string(), u)),
    "@" <u: Unary> => Box::from(Node::Unary("@".to_string(), u)),
    "@@" <u: Unary> => Box::from(Node::Unary("@@".to_string(), u)),
}

Signature: Option<Vec<ParameterNode>> = {
    => None,
    "|" "|" Separator? => Some(vec![]),
    "|" <s: ParameterList> "|" Separator? => Some(s),
}

ParameterList: Vec<ParameterNode> = {
    Parameter => vec![<>],
    <mut l: ParameterList> Separator? <p: Parameter> => {l.push(p); l},
}

Parameter: ParameterNode = {
    <l: Label> <d: Default> => ParameterNode::Parameter(l.to_string(), None, d),
    <n: Label> Colon <t: Item> <d: Default> => ParameterNode::Parameter(n.to_string(), Some(t), d),
    "@" <l: Label> => ParameterNode::Unnamed(l.to_string()),
    "@@" <l: Label> => ParameterNode::Named(l.to_string()),
}

Default: Option<Node> = {
    => None,
    "=" <l: Logical> => Some(*l),
}

Item: Box<Node> = {
    Label => Node::parse_label(<>),
    <l: Regex> => Box::from(Node::Regex(l[3..l.len()-1].to_string())),
    Field => Box::from(Node::Field(<>.to_string())),
    <l:QuotedLabel> => Box::from(Node::Label(l[1..l.len()-1].to_string())),
    QuotedString => Box::from(Node::String(<>.to_string())),
    Integer => Box::from(Node::Integer(i128::from_str(<>.replace("_", "").as_str()).unwrap())),
    Float => Box::from(Node::Float(f64::from_str(<>.replace("_", "").as_str()).unwrap())),
    Flag => Box::from(Node::Assignment(Box::from(Node::Label(<>[2..].to_string())), "=".to_string(), Box::from(Node::Label("true".to_string())))),
    <i: Item> "[" <e: Assignment> "]" => Box::from(Node::GetItem(i, e)),
    <i: Item> Colon <l: AnyLabel> => Box::from(Node::GetAttr(i, l)),
    "{" Separator? <s: Signature> <l: JobListWithoutSeparator> "}" => Box::from(Node::Closure(s, l)),
    "(" <j:Job> ")" => Box::from(Node::Substitution(j)),
}

AnyLabel: String = {
    Label => <>.to_string(),
    <l: QuotedLabel> => l[1..l.len()-1].to_string(),
}

match {
    r"(and|or)" => LogicalOperator,
    r"(typeof|neg|not)" => UnaryOperator,
} else {
    ":" => Colon,
    r"( |\t|\\\n)+" => {},
    r"#[^\n]*" => {},
    r"(>=|<=|>|<|==|!=|=~|!~)" => ComparisonOperator,
    r"(\*|//)" => FactorOperator,
    r"(\+|-)" => TermOperator,
    r#""([^\\"]|\\.)*""# => QuotedString,
    r"([\._a-zA-Z%\?~][\._0-9a-zA-Z%\?]*(/[\._0-9a-zA-Z%\?]+)*/?|/[\._0-9a-zA-Z%\?]+(/[\._0-9a-zA-Z%\?]+)*/?|/)" => Label,
    r"--[_0-9a-zA-Z]+" => Flag,
    r"\^[\._a-zA-Z][\._a-zA-Z0-9]*" => Field,
    r#"'([^\\']|\\.)*'"# => QuotedLabel,
    r#"re"([^"]|\\.)*""# => Regex,
    r"(;|\n)( |\t|;|\n|#[^\n]*)*" => Separator,
    r"[0-9][0-9_]*" => Integer,
    r"[0-9][0-9_]*\.[0-9_]+" => Float,
    _
}
